@top Program { (declaration Semi)* }

Semi { ";" | insertSemi }

declaration {
    Type |
    Predicate |
    SymPred |
    Function |
    Constructor |
    Subtype
}

kw<term> { @specialize[@name={term}]<Identifier, term> }

Type {
    kw<"type"> (Identifier | Subtype)
}

Predicate {
    kw<"predicate"> Identifier ParamList
}

SymPred {
    kw<"symmetric"> Predicate
}

Function {
    kw<"function"> Identifier ParamList "->" Identifier Identifier?
}

Constructor {
    kw<"constructor"> Identifier ParamList ("->" Identifier Identifier?)?
}

Subtype {
    Identifier "<:" InheritanceList
}

// Named for use in identifying autocomplete
InheritanceList {
    commaSep1<Identifier>
}

ParamList {
    "(" commaSep<Identifier (Identifier)?> ")"
  }

commaSep<content> {
    (content (Sep content)*)?
  }


commaSep1<content> {
    content (Sep content)*
}

// Named for use in identifying autocomplete
Sep {
    ","
}

@tokens {
	identifierChar { @asciiLetter | $[_$\u{a1}-\u{10ffff}] }
    word { identifierChar (identifierChar | @digit)* }
    Identifier { word }
    LineComment[@export] { "--" ![\n]* }
    spaces[@export] { $[\u0009 \u000b\u00a0]+ }
    newline[@export] { $[\r\n] }
}

@skip {} {
BlockComment { "/*" (blockCommentContent | blockCommentNewline)* blockCommentEnd }
}

@local tokens {
blockCommentEnd { "*/" }
blockCommentNewline { "\n" }
@else blockCommentContent
}

@skip { spaces | newline| LineComment | BlockComment }
@external tokens insertSemicolon from "./domainTokens.js" { insertSemi }
@context trackNewline from "./domainTokens.js"

@detectDelim
